/*
 * Copyright 2024, the wasm-sqlite-open-helper project authors and contributors. Please see the AUTHORS file
 * for details. Use of this source code is governed by the Apache 2.0 license that can be found in the LICENSE file.
 * SPDX-License-Identifier: Apache-2.0
 */

@file:Suppress("OPT_IN_USAGE")

plugins {
    id("at.released.weh.gradle.lint.android-lint-noagp")
    id("at.released.weh.gradle.multiplatform.kotlin")
}

group = "at.released.weh"

kotlin {
    jvm()
    js(IR) {
        browser()
        nodejs()
    }
    wasmJs {
        browser()
        nodejs()
    }
    iosSimulatorArm64()
    iosArm64()
    iosX64()
    linuxArm64()
    linuxX64()
    macosArm64()
    macosX64()
    mingwX64()

    applyDefaultHierarchyTemplate()

    sourceSets {
        val appleAndLinuxMain by creating {
            dependsOn(nativeMain.get())
        }
        appleMain.get().dependsOn(appleAndLinuxMain)
        linuxMain.get().dependsOn(appleAndLinuxMain)

        commonMain.dependencies {
            api(kotlin("test"))
            api(libs.assertk)
            api(libs.kotlinx.io)
        }
    }
}
